OUTPUT_ARCH(riscv)
ENTRY(_start)

KERNEL_LMA  = 0x81000000;    
KERNEL_VMA  = 0xFFFFFFC000000000;  
KERNEL_OFFSET = KERNEL_VMA - KERNEL_LMA;
PAGE_SIZE   = 4K;

SECTIONS
{
  . = KERNEL_VMA;
  
  _kernel_start = .;

  .text : AT(ADDR(.text) - KERNEL_OFFSET) ALIGN(PAGE_SIZE) {
    _text_start = .;
    KEEP(*(.text.entry))
    *(.text .text.*)
    _text_end = .;
  }


  .rodata : AT(ADDR(.rodata) - KERNEL_OFFSET) ALIGN(PAGE_SIZE) {
    _rodata_start = .;
    *(.rodata .rodata.*)
    *(.srodata .srodata.*)
    _rodata_end = .;
  }
  

  .data : AT(ADDR(.data) - KERNEL_OFFSET) ALIGN(PAGE_SIZE) {
    _data_start = .;
    *(.data .data.*)
    *(.sdata .sdata.*)
    PROVIDE(__global_pointer$ = .);
    *(.got .got.*)
    _data_end = .;
  }

  .bss : AT(ADDR(.bss) - KERNEL_OFFSET) ALIGN(PAGE_SIZE) {
    _bss_start = .;
    *(.bss .bss.*)
    *(COMMON)
    _bss_end = .;
  }

  .stack (NOLOAD) : AT(ADDR(.stack) - KERNEL_OFFSET) ALIGN(PAGE_SIZE){
      _stack_start = .;
      . += 4K*4;
      _stack_top = .;
  } 

  _kernel_end = .;
}

