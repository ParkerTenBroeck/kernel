OUTPUT_ARCH(riscv)
ENTRY(_start)

KERNEL_LOAD_ADDR = 0x81000000;

# 2G of space
KERNEL_LINK_ADDR = 0x0 - 2 * 1024 * 1024 * 1024;

OFFSET = KERNEL_LINK_ADDR - KERNEL_LOAD_ADDR; 

PAGE_SIZE   = 4K;

SECTIONS
{ 
  . = KERNEL_LINK_ADDR;


  _kernel_start = .;

  . += SIZEOF_HEADERS;
  

  .text : AT(ADDR(.text) - OFFSET) ALIGN(PAGE_SIZE) {
    _text_start = .;
    KEEP(*(.text.entry))
    *(.text .text.*)
    _text_end = .;
  }


  .rodata : AT(ADDR(.rodata) - OFFSET) ALIGN(PAGE_SIZE) {
    _rodata_start = .;
    *(.rodata .rodata.*)
    *(.srodata .srodata.*)
  }

  .rela.dyn : AT(ADDR(.rela.dyn) - OFFSET) ALIGN(8) {
    __rela_dyn_start = .;
    KEEP(*(.rela.dyn))
    #KEEP(*(.rela.*))
    __rela_dyn_end = .;
  }
  
  # we include rela in ro data
  _rodata_end = .;

  .data : AT(ADDR(.data) - OFFSET) ALIGN(PAGE_SIZE) {
    _data_start = .;
    *(.data .data.*)
    *(.sdata .sdata.*)
    *(.got*)
    _data_end = .;
  }


  .symtab (PROGBITS) : AT(ADDR(.symtab) - OFFSET) { 
    KEEP(*(.symtab)) 
  }				
  .strtab (PROGBITS) : AT(ADDR(.strtab) - OFFSET) { 
    KEEP(*(.strtab)) 
  }	

  .bss (NOLOAD) : AT(ADDR(.bss) - OFFSET) ALIGN(PAGE_SIZE) {
    _bss_start = .;
    *(.bss .bss.*)
    *(.sbss .sbss.*)
    *(COMMON)
    _bss_end = .;
  }

  .stack (NOLOAD) : AT(ADDR(.stack) - OFFSET) ALIGN(PAGE_SIZE){
      _stack_start = .;
      . += 4K*16;
      _stack_top = .;
  } 

  _kernel_end = .;

   /DISCARD/ : {
        *(.comment*)
        *(.eh_frame*)
        *(.gcc_except_table*)
        *(.note*)
        *(.rel.eh_frame*)
    }
}

